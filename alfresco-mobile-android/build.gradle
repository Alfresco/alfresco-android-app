/*
 *  Copyright (C) 2005-2016 Alfresco Software Limited.
 *
 *  This file is part of Alfresco Mobile for Android.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def version_code      = System.getenv('VERSION_CODE')      ?: VERSION_CODE
def keystore_file     = System.getenv('KEYSTORE_FILE')     ?: RELEASE_STORE_FILE
def keystore_password = System.getenv('KEYSTORE_PASSWORD') ?: RELEASE_STORE_PASSWORD
def key_alias         = System.getenv('KEY_ALIAS')         ?: RELEASE_KEY_ALIAS
def key_password      = System.getenv('KEY_PASSWORD')      ?: RELEASE_KEY_PASSWORD
def gservices_cfg     = System.getenv('GOOGLE_SERVICES_FILE') ?: GOOGLE_SERVICES_FILE

android {
    compileSdkVersion 31
    buildToolsVersion "28.0.3"

    flavorDimensions "default"

    defaultConfig {
        applicationId APPLICATION_ID

        minSdkVersion 19
        targetSdkVersion 31
        versionCode version_code as Integer
        multiDexEnabled true

        versionName VERSION_NAME

        manifestPlaceholders = [PROVIDER_AUTHORITY: PROVIDER_AUTHORITY]
        resValue "string", "account_name", ACCOUNT_ID
        resValue "string", "sync_adapter", SYNC_ADAPTER
        resValue "string", "favorites_adapter", FAVORITES_ADAPTER

        defaultPublishConfig DEFAULT_PUBLISH
        resValue "string", "oauth_api_key", OAUTH_API_KEY
        resValue "string", "oauth_api_secret", OAUTH_API_SECRET
        resValue "string", "oauth_callback", OAUTH_CALLBACK
        resValue "string", "oauth_scope", OAUTH_SCOPE

        resValue "string", "ga_trackingId", TRACKER_ID
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {

        custom {
            //Add here your custom sign configuration
        }


        release {
            storeFile file(keystore_file)
            storePassword keystore_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        debug {
            // Important : In case of custom application
            // Don't forget to change all values to custom debug values
            applicationIdSuffix ".debug"

            manifestPlaceholders = [PROVIDER_AUTHORITY: PROVIDER_AUTHORITY_DEBUG]
            resValue "string", "account_name", ACCOUNT_ID_DEBUG
            resValue "string", "sync_adapter", SYNC_ADAPTER_DEBUG
            resValue "string", "favorites_adapter", FAVORITES_ADAPTER_DEBUG

            resValue "string", "ga_trackingId", TRACKER_ID_DEBUG
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def task = project.tasks.create("copy${variant.name.capitalize()}Apk", Copy)

                    def sourceDir = "${project.buildDir}/outputs/apk/${variant.getFlavorName()}" + "/${variant.buildType.name}/"
                    def destDir = "${project.buildDir}/outputs/apk/"
                    def apkName = output.outputFileName.replace(".apk", "-${variant.versionName}.apk")

                    println ">>>Pre-copy ${apkName}" +
                            "\nsource dir: ${sourceDir}" +
                            "\ndest dir: ${destDir}" +
                            "\napk: ${apkName}"

                    task.from file(sourceDir)
                    task.into file(destDir)
                    task.rename output.outputFileName, apkName
                    task.doFirst {
                        println ">>>Copying ${apkName}" +
                                "\nsource dir: ${sourceDir}" +
                                "\ndest dir: ${destDir}"
                    }
                    task.doLast{
                        println ">>>Copied ${apkName} with success!" +
                                "\nsource dir: ${sourceDir}" +
                                "\ndest dir: ${destDir}"

                        if (!variant.name.toLowerCase().contains("debug")) {
                            delete file("${project.buildDir}\\outputs\\apk\\" + variant.getFlavorName())
                        }
                    }
                    variant.getAssembleProvider().get().finalizedBy(task)
                }
            }
        }
    }


    productFlavors {
        //CUSTOM EXTENSION FLAVOR
        custom {
            //Add here custom configuration

            dependencies {
                //Add here 3rd party dependencies
            }
        }

        //PLAY STORE FLAVOR
        store {
            dependencies {
                //DEBUG BUILD TYPE
                debugImplementation project(':extensions:analytics')
                debugImplementation project(':extensions:tools')


                storeImplementation project(':extensions:samsung')
                storeImplementation project(':extensions:scansnap')
                storeImplementation project(':extensions:analytics')
            }
        }

        //MOBILE IRON FLAVOR
        mobileiron {
            dependencies {
                mobileironCompile project(':mdm:mobileiron')
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
    }
}

project.afterEvaluate {
    if (file("${gservices_cfg}").exists()) {
        println "Copying ${gservices_cfg} into ${projectDir}/google-services.json"
        copy {
            from file("${gservices_cfg}")
            into file("${projectDir}")
            rename '(.*)', 'google-services.json'
        }
    } else {
        println "Using dummy ${projectDir}/google-services.json"
    }
}

dependencies {
    //COMMON TO ALL VARIANTS
    api 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    releaseImplementation 'com.google.firebase:firebase-analytics:20.1.1'
    releaseImplementation 'com.google.firebase:firebase-crashlytics:18.2.9'
    api project(':platform:foundation')
}
